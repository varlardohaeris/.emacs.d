;; This is canonical BNF notation represented with a BNF.
;;
;; The syntax of the BNF has been carefully copied from Revised ALGOL 60 report
;; <http://www.masswerk.at/algol60/report.htm>.

 <syntax>         ::= <rule> | <rule> <syntax>
 <rule>           ::= <opt-ws> < <rule-name> > <opt-ws>
                      ::= <opt-ws> <expression> <line-end>
 <opt-ws>         ::= <space> <opt-ws> | <empty>
 <expression>     ::= <list> | <list> <opt-ws> <or> <opt-ws>
                      <expression>
 <line-end>       ::= <opt-ws> <EOL> | <line-end> <line-end>
 <list>           ::= <term> | <term> <opt-ws> <list>
 <term>           ::= <literal> | < <rule-name> >
 <literal>        ::= <character> <text>
 <character>      ::= <letter> | <digit> | <symbol>
 <letter>         ::= A | B | C | D | E | F | G | H | I | J | K | L |
                      M | N | O | P | Q | R | S | T | U | V | W | X |
                      Y | Z | a | b | c | d | e | f | g | h | i | j |
                      k | l | m | n | o | p | q | r | s | t | u | v |
                      w | x | y | z
 <digit>          ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
 <symbol>         ::= <or>  | <space>   | ! | # | $ | % | & | ( | ) |
                      * | + | , | - | . | / | : | ~ | > | = | < | ? |
                      @ | [ | \ | ] | ^ | _ | ` | ' | " | { | } | ;
 <rule-name>      ::= <letter> | <rule-name> <rule-char>
 <rule-char>      ::= <letter> | <digit> | -
 <space>          ::= <space, tab or new line character>
 <or>             ::= |
 <empty>          ::=

;; Local Variables:
;; indent-tabs-mode: nil
;; End:
